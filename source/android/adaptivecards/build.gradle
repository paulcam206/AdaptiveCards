apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14 -fexceptions -frtti -DNO_LOCALE_SUPPORT"
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

def projectInfo = {
    name = 'Android Adaptive Cards Library'
    description = 'Android Adaptive Cards Lib'
    url = 'https://github.com/Microsoft/AdaptiveCards'

    scm {
        url = 'https://github.com/Microsoft/AdaptiveCards'
    }

    licenses {
        license {
            name = 'Adaptive Cards Binary EULA'
            url = 'https://github.com/microsoft/AdaptiveCards/blob/main/source/EULA-Non-Windows.txt'
            distribution = 'repo'
        }
    }

    developers {
        developer {
            id = "microsoft"
            name = "adaptivecards"
        }
    }

    organization {
        name = 'Microsoft'
        url = 'https://microsoft.com/'
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'io.adaptivecards'
                artifactId = 'adaptivecards-android'

                version = "${acVersionMajor}.${acVersionMinor}.${acVersionPatch}"

                // Append pre-release identifier, if AC_PRERELEASE_TAG is non-empty
                if(System.getenv('AC_PRERELEASE_TAG')) {
                    version += "-${System.getenv('AC_PRERELEASE_TAG')}"
                }

                // Append pipeline build number, if AC_APPEND_BUILD_NUMBER is true
                if(System.getenv('BUILD_BUILDNUMBER') && Boolean.valueOf(System.getenv('AC_APPEND_BUILD_NUMBER'))) {
                    version += ".${System.getenv('BUILD_BUILDNUMBER')}"
                }

                pom projectInfo
            }

        }

        repositories {
            // Publish to Azure Artifacts feed
            maven {
                name = 'azuredevops'
                url = 'https://microsoft.pkgs.visualstudio.com/_packaging/AdaptiveCards/maven/v1'
                credentials {
                    username "AZURE_ARTIFACTS"
                    password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") ?: "${azureArtifactsGradleAccessToken}"
                }
            }
            // Save to local directory "artifact"
            maven {
                name = 'localDirectory'
                url = "artifact"
            }
            // TODO: Add SonaType OSS repository to publish directly
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android:flexbox:1.0.0'
    testImplementation 'junit:junit:4.12'
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
